buildscript {
    repositories {
        mavenCentral()
        maven {
            url kotlinCompilerRepo
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$buildKotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$gradlePluginVersion"
    }
}

repositories {
    maven {
        url kotlinCompilerRepo
    }
}

//TODO: property
def nativeWarmup = 10

ext."konan.home" = distDir

apply plugin: 'application'
apply plugin: 'konan'

checkKonanCompiler.dependsOn ':dist'

konanArtifacts {
    program('Ring') {
        srcDir 'src/main/kotlin'
        srcDir 'src/main/kotlin-native'
        enableOptimizations true
        extraOpts '--llc'
        extraOpts '--verbose', 'object_files'
    }
}

private void dumpReport(String name, ByteArrayOutputStream output) {
    new File("${buildDir.absolutePath}/${name}.txt").withOutputStream {
        it.write(output.toByteArray())
    }
}

task konanRun(type: Exec) {
    dependsOn 'build'
    def output = new ByteArrayOutputStream()
    commandLine konanArtifacts.Ring.getByTarget('host').artifact.absolutePath, "$nativeWarmup"
    standardOutput = output
    doLast {
        dumpReport('konanReport', output)
    }
}

startScripts{
    setEnabled(false)
}

task bench(type:DefaultTask) {
    dependsOn konanRun
    doLast {
        def konanReport = new Report(project.file("build/konanReport.txt"))
        def absoluteAverage = "none"
        konanReport.report
                .sort { konanReport.report[it.key].mean / it.value.mean }
                .each { k, v ->
            def konanValue = konanReport.report[k]
            def ratio = konanValue.mean / v.mean
            def formattedKonanValue = String.format('%.4f Â± %.4f us', konanValue.mean / 1000, konanValue.stdDev / 1000)
            if (k == 'RingAverage') {
                absoluteAverage = formattedKonanValue
            } else {
                println("$k : absolute = $formattedKonanValue")
            }
            if (System.getenv("TEAMCITY_BUILD_PROPERTIES_FILE") != null)
                println("##teamcity[buildStatisticValue key='$k' value='$ratio']")
        }
        println()
        println("Average Ring score: absolute = $absoluteAverage")
    }
}

class Results {
    Double mean
    Double stdDev

    Results(Double mean, Double stdDev) {
        this.mean = mean
        this.stdDev = stdDev
    }
}

class Report {
    Map<String, Results> report = new TreeMap()

    Report(File path) {
        path.readLines().drop(3).findAll { it.split(':').length == 3 }.each {
            def p = it.split(':')
            report.put(p[0].trim(), new Results(Double.parseDouble(p[1].trim()), Double.parseDouble(p[2].trim())))
        }
    }
}
